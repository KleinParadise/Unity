```HLSL
Shader "Custom/BasicDiffuse" {
	Properties {
		_EmissiveColor ("Emissive Color", Color) = (1,1,1,1)
		_AmbientColor  ("Ambient Color", Color) = (1,1,1,1)
		_MySliderValue ("This is a Slider", Range(0,10)) = 2.5
	}
	SubShader {
		Tags { "RenderType"="Opaque" }
        LOD 200
        CGPROGRAM
        #pragma surface surf BasicDiffuse
        
       	//We need to declare the properties variable type inside of the
        //CGPROGRAM so we can access its value from the properties block.
        float4 _EmissiveColor;
        float4 _AmbientColor;
        float _MySliderValue;
        
        struct Input
        {
          	float2 uv_MainTex;
        };
        
        void surf (Input IN, inout SurfaceOutput o)
        {
            //We can then use the properties values in our shader
            float4 c;
            c =  pow((_EmissiveColor + _AmbientColor), _MySliderValue);
            o.Albedo = c.rgb;
            o.Alpha = c.a;
        }
        
        inline float4 LightingBasicDiffuse (SurfaceOutput s, fixed3 lightDir, fixed atten)
       	{
       	  	float difLight = max(0, dot (s.Normal, lightDir));
       	  	float4 col;
       	  	col.rgb = s.Albedo * _LightColor0.rgb * (difLight * atten * 2);
       	  	col.a = s.Alpha;
       	  	return col;
		}
        
		ENDCG
	} 
	FallBack "Diffuse"
}
```

创建了一个Shader时，Untiy为我们指定了一个默认的光照模型即Lambert（在Lighting.cginc中定义）。即通过``` #pragma surface surf Lambert ```来指定默认的
Lambert光照模型进行渲染。通过修改该编译指令 ```#pragma surface surf BasicDiffuse ```告诉shader使用名叫BasicDiffuse的光照模型进行渲染。  

为了创建一个新的光照模型，我们需要声明一个新的光照模型函数。在编译指令中声明了BasicDiffuse，然后定义了一个LightingBasicDiffuse的函数。
这两者之间存在对应关系,即为Lighting<自定义的光照模型名称>。 

unity提供了三种可供选择的光照模型函数。  

函数 | 作用
------------ | -------------
half4 LightingName (SurfaceOutput s, half3 lightDir, half atten){} | 正向渲染,不考虑view direction（观察角度。
half4 LightingName (SurfaceOutput s, half3 lightDir, half3 viewDir, half atten){} | 正向渲染,考虑view direction（观察角度。
half4 LightingName_PrePass (SurfaceOutput s, half4 light){} | 延迟渲染

对LightingBasicDiffuse实现自定义光照模型函数的解释  
```HLSL
inline float4 LightingBasicDiffuse (SurfaceOutput s, fixed3 lightDir, fixed atten)
{
    float difLight = max(0, dot (s.Normal, lightDir));
    float4 col;
    col.rgb = s.Albedo * _LightColor0.rgb * (difLight * atten * 2);
    col.a = s.Alpha;
    return col;
}
```
SurfaceOutput s 该参数对应surf函数输出的结果SurfaceOutput o。经过surf函数，计算输出了s的Albedo（反射率）和Alpha（透明度）。  
lightDir对应该点的光照方向。  
atten表明了光线的衰减率。  

``` float difLight = max(0, dot (s.Normal, lightDir)); ``` 通过dot函数和max函数，计算到达该点的光照值（由于dot函数的两个参数都是单位向量，也可以
理解成是入射光线的角度的余弦值，角度越大，余弦值越小，进入人眼的光线也就越少，物体看起来也就越暗）。由于光线有可能从相反射入，因此通过dot得到的值有可能
是负数。如果不用max加以限制，之后将会得到非预期的效果，如全黑等。

接下来计算颜色值col。  
col的rgb由三个部分计算而得:  
第一个部分是surface本身的反射率，因为反射率越大，进入人眼的光线就越多，颜色也就越鲜亮  
第二个是_LightColor0.rgb。_LightColor0是Unity内置变量，我们可以使用它得到场景中光源的颜色等  
最后便是利用第一步中得到的光照值和衰减率的乘积





