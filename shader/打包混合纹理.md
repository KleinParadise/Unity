### 灰度图的理解
灰度图中没有彩色只有灰色，该灰色介于白色和黑色之间的颜色。
### 灰度图特性
1. 灰度通道中的纯白，代表了该色光在此处为最高亮度，亮度级别是1。
2. 灰度通道中的纯黑，代表了该色光在此处完全不发光，亮度级别是0。
3. 介于纯黑纯白之间的灰度，代表了不同的发光程度，亮度级别介于0至1之间。
4. 灰度中越偏白的部分，表示色光亮度值越高，越偏黑的部分则表示亮度值越低。

### rgba四通道的灰度图与其彩色图对应关系
rgba四通道分别代表了红、绿、蓝、和透明度四种颜色值的浓度。   
![Image of deque](https://github.com/KleinParadise/Unity/blob/master/shader/pic/hunhe.png)  

如上图所示,在R通道的灰度图中，越亮的部分，即颜色越白的部分，表示红色光在此处越亮，亮度级别越接近1（亮度范围在这里为0到1），表现为颜色越红，与右边的彩色图对应（当然还会和蓝绿进行颜色混合得到最后彩色效果）。相反，越黑的地方表示红色越弱，亮度级别越接近0。

### 将纹理打包的原因
应用中，textures的数目将很大程度上影响应用的性能。可利用shader将多个纹理合并为一个纹理,以此来减少drawcall,优化性能。

### 纹理混合的应用
terrain Shaders（地形渲染）应用广泛,地形渲染涉及一张纹理与多张纹理的混合

### TextureBlending shader
```HLSL
Shader "Custom/TextureBlending" {
	Properties {
		_MainTint ("Diffuse Tint", Color) = (1,1,1,1)
		
		//Add the properties below so we can input all of our textures
		_ColorA ("Terrain Color A", Color) = (1,1,1,1)
		_ColorB ("Terrain Color B", Color) = (1,1,1,1)
		_RTexture ("Red Channel Texture", 2D) = ""{}
		_GTexture ("Green Channel Texture", 2D) = ""{}
		_BTexture ("Blue Channel Texture", 2D) = ""{}
		_ATexture ("Alpha Channel Texture", 2D) = ""{}
		_BlendTex ("Blend Texture", 2D) = ""{}
	}
	SubShader {
		Tags { "RenderType"="Opaque" }
		LOD 200
		
		CGPROGRAM
		#pragma surface surf Lambert

		float4 _MainTint;
		float4 _ColorA;
		float4 _ColorB;
		sampler2D _RTexture;
		sampler2D _GTexture;
		sampler2D _BTexture;
		sampler2D _BlendTex;
		sampler2D _ATexture;

		struct Input {
			float2 uv_RTexture;
			float2 uv_GTexture;
			float2 uv_BTexture;
			float2 uv_ATexture;
			float2 uv_BlendTex;
		};

		void surf (Input IN, inout SurfaceOutput o) {
			//Get the pixel data from the blend texture
			//we need a float 4 here because the texture 
			//will return R,G,B,and A or X,Y,Z, and W
			float4 blendData = tex2D(_BlendTex, IN.uv_BlendTex);
			
			//Get the data from the textures we want to blend
			float4 rTexData = tex2D(_RTexture, IN.uv_RTexture);
			float4 gTexData = tex2D(_GTexture, IN.uv_GTexture);
			float4 bTexData = tex2D(_BTexture, IN.uv_BTexture);
			float4 aTexData = tex2D(_ATexture, IN.uv_ATexture);
			
			//No we need to contruct a new RGBA value and add all 
			//the different blended texture back together
			float4 finalColor;
			finalColor = lerp(rTexData, gTexData, blendData.g);
			finalColor = lerp(finalColor, bTexData, blendData.b);
			finalColor = lerp(finalColor, aTexData, blendData.a);
			finalColor.a = 1.0;
			
			//Add on our terrain tinting colors
			float4 terrainLayers = lerp(_ColorA, _ColorB, blendData.r);
			finalColor *= terrainLayers;
			finalColor = saturate(finalColor);
				
			o.Albedo = finalColor.rgb * _MainTint.rgb;
			o.Alpha = finalColor.a;
		}
		ENDCG
	} 
	FallBack "Diffuse"
}
```
1. 准备4张用于混合的textures。这里准备一张草地（grass）、泥土（dirt）、小石子（rocky dirt）、石头（rock）的texture。如下图所示:  
![Image of deque](https://github.com/KleinParadise/Unity/blob/master/shader/pic/four.png)  

2. 准备一张混合的texture,它是由多个灰度图混合而成的。作用是告诉我们以上四张texture在目标地形上是如何分布的。混合的texture图如下所示:  
![Image of deque](https://github.com/KleinParadise/Unity/blob/master/shader/pic/hunhe.png)  

3. 将上述5个纹理在shader的properties新增对应的sampler2D对象。同时新增两个颜色properties，和一个用于调整整体地形颜色的值三个颜色变量。分别为
```_ColorA,_ColorB,_MainTint```
4. 在SubShader中创建8个变量，分别对应Properties中的8个properties，以建立和它们之间的链接。
5. 为了使用不同textures(地形的平铺率),修改```struct Input ```的结构体。以便获得上述5纹理的uv坐标
6. 读取每张图的纹理数据,保存在对应的变量中
    ```HLSL
    float4 blendData = tex2D(_BlendTex, IN.uv_BlendTex);			
    //Get the data from the textures we want to blend
    float4 rTexData = tex2D(_RTexture, IN.uv_RTexture);
    float4 gTexData = tex2D(_GTexture, IN.uv_GTexture);
    float4 bTexData = tex2D(_BTexture, IN.uv_BTexture);
    float4 aTexData = tex2D(_ATexture, IN.uv_ATexture);
    ```
7. 使用lerp函数将四张texture混合。
    ```HLSL
    float4 finalColor;
    finalColor = lerp(rTexData, gTexData, blendData.g);
    finalColor = lerp(finalColor, bTexData, blendData.b);
    finalColor = lerp(finalColor, aTexData, blendData.a);
    finalColor.a = 1.0;
    ```
 8. 使用blending texture的R通道值混合两个颜色色调值，并将结果与之前的混合值相乘。
     ```HLSL
    float4 terrainLayers = lerp(_ColorA, _ColorB, blendData.r);
    finalColor *= terrainLayers;
    finalColor = saturate(finalColor);

    o.Albedo = finalColor.rgb * _MainTint.rgb;
    o.Alpha = finalColor.a;
    ```
    
### lerp函数的理解
lerp函数是CGFX标准库中内置函数。该函数允许我们从参数一合参数二之间挑选一个值，并使用参数三作为混合程度。它的工作原理如下所示：  
![Image of deque](https://github.com/KleinParadise/Unity/blob/master/shader/pic/lerp1.png)  

根据上述公式可知f的值越大，则越接近b的值

从grass texture和dirt texture中提取颜色值，并使用blend texture对应的G通道值进行lerp运算,从下图可了解lerp函数的实现过程:  
![Image of deque](https://github.com/KleinParadise/Unity/blob/master/shader/pic/lerp2.png)  

 
